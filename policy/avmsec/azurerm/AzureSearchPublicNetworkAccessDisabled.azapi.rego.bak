package avmsec

import rego.v1

valid_azurerm_search_service_public_network_access_disabled(resource) if {
    resource.values.public_network_access_enabled == false
}

valid_azapi_search_service_public_network_access_disabled(resource) if {
    resource.body.properties.publicNetworkAccess == "disabled"
}

deny_AVM_SEC_124 contains reason if {
    resource := data.utils.resource(input, "azurerm_search_service")[_]
    not valid_azurerm_search_service_public_network_access_disabled(resource)

    reason := sprintf("avmsec/AVM_SEC_124: Ensure that Azure Cognitive Search disables public network access. Resource %s has public_network_access_enabled set to true, but it should be false. https://github.com/bridgecrewio/checkov/blob/main/checkov/terraform/checks/resource/azure/AzureSearchPublicNetworkAccessDisabled.py", [resource.address])
}

deny_AVM_SEC_124 contains reason if {
    resource := data.utils.resource(input, "azapi_resource")[_]
    resource.type == "Microsoft.Search/searchServices/2024-06-01-preview"
    not valid_azapi_search_service_public_network_access_disabled(resource)

    reason := sprintf("avmsec/AVM_SEC_124: Ensure that Azure Cognitive Search disables public network access. Resource %s has public_network_access_enabled set to true, but it should be false. https://github.com/bridgecrewio/checkov/blob/main/checkov/terraform/checks/resource/azure/AzureSearchPublicNetworkAccessDisabled.py", [resource.address])
}
